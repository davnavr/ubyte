.module Inheritance
.version 0 0 700000
.format 0 0

.identifier @id_namespace Example
.identifier @id_entry_point Main
.identifier @id_base_class Counter
.identifier @id_base_counter counter
.identifier @id_base_increment increment
.identifier @id_class_child_decrement CountDownCounter

.signature @sig_int type s32
.signature @sig_obj_base type class @class_base
.signature @sig_obj_child_decrement type class @class_child_decrement

.signature @sig_main method (@sig_int) ()
.signature @sig_base_constructor method () (@sig_obj_base)
.signature @sig_base_increment method () (@sig_obj_base)
.signature @sig_child_decrement_decrement method () (@sig_obj_child_decrement)

.code @code_main {
    .block $ENTRY {
        %t_class_0 = obj.new @method_base_constructor ()
        call @method_base_increment (%t_class_0)
        %t_incr_0 = obj.fd.ld @field_base_counter %t_class_0

        %t_class_1 = obj.new @method_decrement_constructor ()
        call @method_decrement_decrement (%t_class_1)
        %t_incr_1 = obj.fd.ld @field_base_counter %t_class_1

        %t_incr_sum = add s32 %t_incr_0 %t_incr_1

        ret %t_incr_sum
    }
}

.code @code_base_constructor {
    .arguments ($a_this)
    .block $ENTRY {
        %t0 = const.zero s32
        obj.fd.st @field_base_counter $a_this %t0
        ret
    }
}

.code @code_base_increment {
    .arguments ($a_this)
    .block $ENTRY {
        %t0 = obj.fd.ld @field_base_counter $a_this
        %t1 = incr s32 %t0
        obj.fd.st @field_base_counter $a_this %t1
        ret
    }
}

.code @code_child_constructor {
    .arguments ($a_this)
    .block $ENTRY {
        call @method_base_constructor ($a_this)
        ret
    }
}

.code @code_child_decrement {
    .arguments ($a_this)
    .block $ENTRY {
        %t0 = obj.fd.ld @field_base_counter $a_this
        %t1 = decr s32 %t0
        obj.fd.st @field_base_counter $a_this %t1
        ret
    }
}

.namespace @namespace_example {
    .name @id_namespace
}

.type @class_base public nonfinal {
    .name @id_base_class
    .namespace @namespace_example

    .method @method_base_constructor public instance constructor {
        .signature @sig_base_constructor
        .body defined @code_base_constructor
    }

    .method @method_base_increment public instance {
        .signature @sig_base_increment
        .body defined @code_base_increment
        .name @id_base_increment
    }

    .method @method_main public {
        .signature @sig_main
        .body defined @code_main
        .name @id_entry_point
    }

    .field @field_base_counter public mutable {
        .type @sig_int
        .name @id_base_counter
    }
}

.type @class_child_decrement public {
    .name @id_class_child_decrement
    .namespace @namespace_example
    .extends @class_base

    .method @method_decrement_constructor public instance constructor {
        .signature @sig_base_constructor
        .body defined @code_child_constructor
    }

    .method @method_decrement_decrement public instance {
        .signature @sig_child_decrement_decrement
        .body defined @code_child_decrement
        .name @id_class_child_decrement
    }
}

.entrypoint @method_main
